; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

; EDIT THESE LINES
#define RRExtPluginName "RRGmail"
#define RRExtPluginFriendlyName "Gmail Notifier Emailer"
#define RRExtPluginVersion "V1.1.1"
#define RRExtPluginPublisher "pierrotm777"
#define RRExtURL "pierrotm777@gmail.com"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{5FDE4D98-5E33-4237-A6AD-510AEA6A4E0B}
AppName=RR Plugin for {#RRExtPluginFriendlyName}
AppVerName=RR Plugin for {#RRExtPluginFriendlyName} {#RRExtPluginVersion}
AppPublisher={#RRExtPluginPublisher}
AppPublisherURL={#RRExtURL}
AppSupportURL={#RRExtURL}
AppUpdatesURL={#RRExtURL}
DefaultDirName={reg:HKLM\Software\RideRunner,Path|{pf}\RideRunner}\Plugins\{#RRExtPluginName}
OutputDir=..\Installer Output
OutputBaseFilename=RR Plugin {#RRExtPluginFriendlyName} {#RRExtPluginVersion} Setup
SourceDir=Source
Compression=lzma
SolidCompression=yes
Uninstallable=yes
;LicenseFile=License.txt

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"

[Tasks]
Name: SSCDL; Description: Install the Sample Skin into Carwings Dynamic Lite

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: "RRGmail.dll"; DestDir: "{code:DataPath}\Plugins\{#RRExtPluginName}"; Flags: ignoreversion
Source: "Google.GData.Client.dll"; DestDir: "{code:DataPath}\Plugins\{#RRExtPluginName}"; Flags: ignoreversion
Source: "Google.GData.Contacts.dll"; DestDir: "{code:DataPath}\Plugins\{#RRExtPluginName}"; Flags: ignoreversion
Source: "Google.GData.Extensions.dll"; DestDir: "{code:DataPath}\Plugins\{#RRExtPluginName}"; Flags: ignoreversion
Source: "OpenPop.dll"; DestDir: "{code:DataPath}\Plugins\{#RRExtPluginName}"; Flags: ignoreversion
Source: "rr.jpg"; DestDir: "{code:DataPath}\Plugins\{#RRExtPluginName}"; Flags: ignoreversion
Source: "Profiles.txt"; DestDir: "{code:DataPath}\Plugins\{#RRExtPluginName}"; Flags: ignoreversion
Source: "Providers.txt"; DestDir: "{code:DataPath}\Plugins\{#RRExtPluginName}"; Flags: ignoreversion
Source: "RRGmail Skin Commands.txt"; DestDir: "{code:DataPath}\Plugins\{#RRExtPluginName}"; Flags: ignoreversion isreadme
Source: "Readme.txt"; DestDir: "{code:DataPath}\Plugins\{#RRExtPluginName}"; Flags: ignoreversion isreadme

; Languages
Source: "Languages\*"; DestDir: "{code:DataPath}\Plugins\{#RRExtPluginName}\Languages"; Flags: recursesubdirs onlyifdoesntexist
; Profiles
Source: "Profiles\*"; DestDir: "{code:DataPath}\Plugins\{#RRExtPluginName}\Profiles"; Flags: recursesubdirs onlyifdoesntexist

; skin
Source: "Sample Skin\*"; DestDir: "{app}\Sample Skin"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Sample Skin\Carwings_Dynamic_Lite\*"; DestDir: "{code:DataPath}\Skins\Carwings_Dynamic_Lite"; Flags: ignoreversion recursesubdirs createallsubdirs; Tasks: SSCDL
;Source: Sample Skin\DFX5\*; DestDir: {code:DataPath}\Skins\DFX5; Tasks: SSCDL; Flags: ignoreversion recursesubdirs createallsubdirs;
;Source: Sample Skin\Uconnect Touch\*; DestDir: {code:DataPath}\Skins\Uconnect Touch; Tasks: SSCDL; Flags: ignoreversion recursesubdirs createallsubdirs;

[Run]
; Regasm
Filename: {dotnet20}\Regasm.exe; Parameters: /tlb /codebase RRGmail.dll; WorkingDir: {app}; StatusMsg: Registering Plugin...; Flags: runhidden


[UninstallRun]
; Regasm
Filename: {dotnet20}\Regasm.exe; Parameters: /u /tlb RRGmail.dll; WorkingDir: {app}; StatusMsg: Un-Registering Plugin...; Flags: runhidden


[UninstallDelete]
Name: {app}\RRGmail.tlb; Type: files
Name: {app}\RRGmail.pdb; Type: files
;Name: {app}\Google.GData.Client.dll; Type: files
;Name: {app}\Google.GData.Contacts.dll; Type: files
;Name: {app}\Google.GData.Extensions.dll; Type: files
;Name: {app}\Newtonsoft.Json.dll; Type: files
Name: {app}\Languages.txt; Type: files
;Name: {app}\Profiles.txt; Type: files
;Name: {app}\RRGmail Skin Commands.txt; Type: files


[Code]
//////////////////////////////////////////////////////////////
// Get PLUG-IN Installation Path, based on Profile Mode
//////////////////////////////////////////////////////////////
function InstallPath(p: String): String;
begin
	// look for rr.ini?
	if FileExists(ExpandConstant('{userdocs}\RideRunner\Config\rr.ini')) then
	begin
		if DirExists(ExpandConstant('{userdocs}\RideRunner\Plugins')) then
			Result := ExpandConstant('{userdocs}\RideRunner\Plugins\{#RRExtPluginName}')
		else
			Result := ExpandConstant('{app}')
	end
	else
		Result := ExpandConstant('{app}')
end;


//////////////////////////////////////////////////////////////
// Get DATA Path, based on Profile Mode
//////////////////////////////////////////////////////////////
function DataPath(p: String): String;
begin
	// look for rr.ini?
	if FileExists(ExpandConstant('{userdocs}\RideRunner\Config\rr.ini')) then
	begin
		if DirExists(ExpandConstant('{userdocs}\RideRunner')) then
			Result := ExpandConstant('{userdocs}\RideRunner')
		else
			Result := ExpandConstant('{reg:HKLM\Software\RideRunner,Path|{pf}\RideRunner}')
	end
	else
		Result := ExpandConstant('{reg:HKLM\Software\RideRunner,Path|{pf}\RideRunner}')
end;
